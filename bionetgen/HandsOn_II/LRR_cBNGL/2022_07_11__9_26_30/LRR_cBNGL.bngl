begin model
begin parameters
  NaV 6.02e8 # Conversion constant: M -> #/um^3
  Vcell 1000 # Typical eukaryotic cell volume ~ 1000 um^3
  Vec   1000*Vcell # Volume of extracellular space 
  d_pm  0.01 # Effective thickness of the plasma membrane (10 nm)
  Acell 1000 # Approximate area of PM
  Vpm   Acell*d_pm # Effective volume of PM 
  lig_conc 1e-8 # Ligand concentration - molar 
  L0 lig_conc*NaV*Vec # number of ligand molecules
  R0 1000 # number of receptor molecules per cell
  kp1 1e6/NaV # #/um^3 1/s: Forward binding rate constant for L-R
  km1 0.01 # 1/s Reverse binding rate constant for L-R
end parameters
begin compartments
  EC 3 Vec 
  PM 2 Vpm EC
end compartments
begin molecule types
    L(r,r)
    R(l)
end molecule types
begin species
  @EC:L(r,r) L0
  @PM:R(l) R0
end species
begin observables
  Molecules LR L(r,r!1).R(l!1)
  Species LRR L(r!1,r!2).R(l!1).R(l!2)
end observables
begin reaction rules
  lrbind1: L(r,r) + R(l) <-> L(r!1,r).R(l!1) kp1, km1
  lrbind2: L(r,r!+) + R(l) <-> L(r!1,r!+).R(l!1) kp1, km1
end reaction rules
end model
simulate({method=>"ode",t_end=>200,n_steps=>200})
